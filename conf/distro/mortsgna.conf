#@--------------------------------------------------------------------
#@TYPE: Distribution
#@NAME: Mortsgna
#@DESCRIPTION: Linux Distribution based on Angstrom
#@MAINTAINER: Andreas MÃ¼ller <schnitzeltony@gmail.com>
#@--------------------------------------------------------------------

DISTRO_VERSION = "v0.1"

# With Angstrom we had DISTRO_TYPE = "debug"/"release". No recipe except
# good old lxdm takes care (yes it was me). OE-Core way is setting
# "debug-tweaks" in IMAGE_FEATURES. So if one wants to login with lxdm as
# root set below in your local.conf:
# DISTRO_TYPE = "debug"

# Set the toolchain type (internal, external) and brand (generic, csl etc.)
TOOLCHAIN_TYPE ?= "internal"
TOOLCHAIN_BRAND ?= ""

PREFERRED_PROVIDER_dbus-glib                     = "dbus-glib"
PREFERRED_PROVIDER-gconf-dbus                    = "gconf"
PREFERRED_PROVIDER_hotplug                       = "systemd"
PREFERRED_PROVIDER_opkg                          ?= "opkg"
PREFERRED_PROVIDER_opkg-native                   ?= "opkg-native"

# Prefer gummiboot over grub
EFI_PROVIDER                                     = "gummiboot"

# blacklist policy
PNBLACKLIST[pn-fso-apm]       = "regular apmd is good enough"
PNBLACKLIST[gconf-dbus]        = "gconf-dbus has been merged back into main GConf"
PNBLACKLIST[gconf-dbus-native] = "gconf-dbus has been merged back into main GConf"

# Add yocto mirros as fallback
MIRRORS += " \
    bzr://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
    cvs://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
    git://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
    gitsm://.*/.* http://downloads.yoctoproject.org/mirror/sources/ \n \
    hg://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
    osc://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
    p4://.*/.*    http://downloads.yoctoproject.org/mirror/sources/ \n \
    svn://.*/.*   http://downloads.yoctoproject.org/mirror/sources/ \n \
"

require conf/distro/include/mortsgna.inc

# uncomment to enable security flags - images tested had issues
#require conf/distro/include/security_flags.inc

# disable static libs (not yet tested - might need recipe adjustments)
require conf/distro/include/no-static-libs.inc

# Toolchain virtuals:
require conf/distro/include/toolchain-${TOOLCHAIN_TYPE}.inc

# Processor specific tunes like hard float ABI
include conf/distro/include/${TARGET_ARCH}-defaults.inc

# Select xserver-xorg and common drivers as default
XSERVER ?= " \
    xserver-xorg \
    xf86-input-libinput \
    xf86-video-modesetting \
"
PREFERRED_PROVIDER_virtual/libx11 = "libx11"
PREFERRED_PROVIDER_virtual/xserver-xf86 = "xserver-xorg"
PREFERRED_PROVIDER_virtual/xserver = "xserver-xorg"

# For X11/Wayland images we install an unpriviledged user by default. If this
# is not desired override DISTRO_GUI_USER
DISTRO_GUI_USER ?= "unpriv-user"

# If you don't want parts of this in your packagegroup-base using images you can put this in the image recipe:
# BAD_RECOMMENDATIONS = "<packages-to-remove>"
# or
# IMAGE_INSTALL_remove = "<packages-to-remove>"
#
# os-release: Distro release info on target
# kernel modules: ship fs modules so you can mount stuff and af-packet so networking works
# util-linux-mount util-linux-umount: busybox mount is broken or not really usable
# less: makes journalctl readable / coloured
#
# DEBUG_APPS: ship strace and procpc to make simple debugging a lot easier
DISTRO_EXTRA_RRECOMMENDS += " \
    os-release \
    \
    kernel-module-vfat \
    kernel-module-ext2 \
    kernel-module-ext3 \
    kernel-module-af-packet \
    \
    strace procps \
    \
    util-linux-mount util-linux-umount \
    less \
"

# Prefer openssh over dropbear when using packagegroup-basic
TASK_BASIC_SSHDAEMON = "openssh-ssh openssh-sshd openssh-scp openssh-sftp openssh-sftp-server"

# Image features strongly suggested
EXTRA_IMAGE_FEATURES += " \
    package-management \
    empty-root-password \
    allow-empty-password \
    post-install-logging \
"

# make x11-base a valid IMAGE_FEATURES
IMAGE_FEATURES[validitems] += "x11-base"
